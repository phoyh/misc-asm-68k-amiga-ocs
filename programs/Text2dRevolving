	include	/includes/exec/exec_lib.i
	include	/includes/intuition/intuition_lib.i
	include	/includes/hardware/offsets.i
	lea	IntName,a1
	clr.l	d0
	move.l	4.w,a6
	jsr	OpenLibrary(a6)
	tst.l	d0
	beq	fertig
	move.l	d0,_IntBase
	move.l	#8000,d0
	move.l	#3,d1
	jsr	AllocMem(a6)
	move.l	d0,ChipMemPointer
	lea	screendata,a0
	move.w	#320,4(a0)
	move.w	#200,6(a0)
	move.w	#1,8(a0)
	move.l	_IntBase,a6
	jsr	OpenScreen(a6)
	move.l	d0,screenpointer
	lea	$DFF000,a5
	lea	OktantCodes,a4
	move.l	screenpointer,a0
	move.l	88(a0),a0
	move.l	8(a0),a0
	lea	sinusliste,a1
	move.w	#45,ydegree
	move.w	#-1,ybonus
	move.w	#1,horpm
	move.w	#1,horbonus
	clr.w	horbase
	move.w	#-1,yplusminus
	clr.w	xdegree
	move.w	#1,xbonus
	move.w	#1,verpm
	move.w	#1,verbonus
	clr.w	verbase
	move.w	#1,xplusminus
	move.w	#$8400,dmacon(a5)
Schleife	move.w	ydegree,d1
	add.w	ybonus,d1
	move.w	d1,ydegree
	move.w	horbonus,d3
	add.w	d3,horbase
	clr.w	d0
	lsl.w	#3,d1
	move.b	(a1,d1.w),d0
	move.w	d0,yfactspeicher
	move.w	ynullpunkt,yaddvar
	mulu	ymitte,d0
	lsr.w	#8,d0
	mulu	yplusminus,d0
	sub.w	d0,yaddvar
	move.w	horbase,d7
	clr.w	d2
	mulu	#3,d7
	move.b	(a1,d7.w),d2
	lsr.w	#3,d2
	mulu	horpm,d2
	sub.w	d2,yaddvar
	move.w	xdegree,d1
	add.w	xbonus,d1
	move.w	d1,xdegree
	move.w	verbonus,d3
	add.w	d3,verbase
	clr.w	d0
	mulu	#12,d1
	divu	#5,d1
	move.b	(a1,d1.w),d0
	move.w	d0,xfactspeicher
	move.w	xnullpunkt,xaddvar
	mulu	xmitte,d0
	lsr.w	#8,d0
	mulu	xplusminus,d0
	sub.w	d0,xaddvar
	move.w	verbase,d7
	clr.w	d2
	lsl.w	#1,d7
	move.b	(a1,d7.w),d2
	lsr.w	#3,d2
	mulu	verpm,d2
	sub.w	d2,xaddvar
	lea	koordsdata,a3
	move.l	(a3)+,zaehler
BlitterFinito3	move.w	dmaconr(a5),d3
	btst	#14,d3
	bne	BlitterFinito3
	move.l	ChipMemPointer,bltdpth(a5)
	clr.w	bltdmod(a5)
	move.w	#%0000000100000000,bltcon0(a5)
	clr.w	bltcon1(a5)
	move.w	#200*64+20,bltsize(a5)
zurp	sub.l	#4,zaehler
	move.w	x2,x1
	move.w	y2,y1
	move.w	yfactspeicher,d2
	move.w	xfactspeicher,d3
	move.w	(a3),d0
	btst	#15,d0
	beq	weiter
	adda.l	#2,a3
	bclr	#15,d0
	sub.l	#4,zaehler
	mulu	d3,d0
	lsr.w	#8,d0
	mulu	xplusminus,d0
	add.w	xaddvar,d0
	move.w	d0,x1
	move.w	(a3)+,d0
	mulu	d2,d0
	lsr.w	#8,d0
	mulu	yplusminus,d0
	add.w	yaddvar,d0
	move.w	d0,y1
weiter	mulu	(a3)+,d3
	lsr.w	#8,d3
	mulu	xplusminus,d3
	add.w	xaddvar,d3
	move.w	d3,x2
	mulu	(a3)+,d2
	lsr.w	#8,d2
	mulu	yplusminus,d2
	add.w	yaddvar,d2
	move.w	d2,y2
	move.w	#$BCA,d6
	move.w	#1,d7
	clr.w	d0
	move.l	x1,d2
	lsr.l	#4,d2
	and.w	#$F000,d2
	add.w	d2,d6
	add.w	d2,d7
	move.w	x1,d2
	move.w	x2,d3
	cmp.w	d2,d3
	bge	l1
	sub.w	d3,d2
	move.w	d2,d4
	bra	l2
l1	sub.w	d2,d3
	move.w	d3,d4
	bset	#1,d0
l2	move.w	y1,d2
	move.w	y2,d3
	cmp.w	d2,d3
	bge	l3
	sub.w	d3,d2
	move.w	d2,d5
	bra	l4
l3	sub.w	d2,d3
	move.w	d3,d5
	bset	#2,d0
l4	cmp.w	d4,d5
	bge	l5
	exg	d4,d5
	bset	#0,d0
l5	lsl.w	d4
	add.b	(a4,d0.w),d7
	move.l	ChipMemPointer,d0
	move.w	y1,d1
	mulu	#40,d1
	add.l	d1,d0
	move.w	x1,d1
	lsr.w	#3,d1
	and.l	#$7FFE,d1
	add.l	d1,d0
	move.w	d4,d1
	sub.w	d5,d1
	tst.w	d1
	bge	wei
	bset	#6,d7
wei	move.w	d5,d2
	tst.w	d2
	bne	wassolls
	move.w	#1,d2
wassolls	lsl.w	#6,d2
	bset	#1,d2
BlitterFinito	move.w	dmaconr(a5),d3
	btst	#14,d3
	bne	BlitterFinito
	move.w	d4,bltbmod(a5)
	move.w	d1,bltaptl(a5)
	move.l	d0,bltcpth(a5)
	move.l	d0,bltdpth(a5)
	sub.w	d5,d1
	move.w	d1,bltamod(a5)
	move.w	d6,bltcon0(a5)
	move.w	d7,bltcon1(a5)
	move.w	#40,bltcmod(a5)
	move.w	#40,bltdmod(a5)
	move.w	#$FFFF,bltafwm(a5)
	move.w	#$8000,bltadat(a5)
	move.w	maske,bltbdat(a5)
	move.w	d2,bltsize(a5)
	tst.l	zaehler
	bne	zurp
	cmpi.w	#180,verbase
	beq	xchange
	tst.w	verbase
	bne	xcheckoff
	not.w	verpm
	bset	#0,verpm+1
xchange	not.w	verbonus
	bset	#0,verbonus+1
xcheckoff	tst.w	xdegree
	bne	xcheckon
	move.w	#1,xbonus
	not.w	xplusminus
	bset	#0,xplusminus+1
	bra	ycheck
xcheckon	cmpi.w	#150,xdegree
	bne	ycheck
	move.w	#-1,xbonus
ycheck	cmpi.w	#120,horbase
	beq	ychange
	tst.w	horbase
	bne	ycheckoff
	not.w	horpm
	bset	#0,horpm+1
ychange	not.w	horbonus
	bset	#0,horbonus+1
ycheckoff	tst.w	ydegree
	bne	ycheckon
	move.w	#1,ybonus
	not.w	yplusminus
	bset	#0,yplusminus+1
	bra	MouseCheck
ycheckon	cmpi.w	#45,ydegree
	bne	MouseCheck
	move.w	#-1,ybonus
MouseCheck	btst	#6,ciaapra
	beq	Finito
BlitterFinito2	move.w	dmaconr(a5),d3
	btst	#14,d3
	bne	BlitterFinito2
	move.l	ChipMemPointer,bltapth(a5)
	move.l	a0,bltdpth(a5)
	clr.w	bltamod(a5)
	clr.w	bltdmod(a5)
	move.w	#%0000100111110000,bltcon0(a5)
	clr.w	bltcon1(a5)
	move.w	#$FFFF,bltalwm(a5)
	move.w	#$FFFF,bltafwm(a5)
	move.w	#200*64+20,bltsize(a5)
	bra	Schleife
Finito	move.l	screenpointer,a0
	move.l	_IntBase,a6
	jsr	CloseScreen(a6)
	move.w	#$400,dmacon(a5)
	move.l	4.w,a6
	move.l	ChipMemPointer,a1
	move.l	#8000,d0
	jsr	FreeMem(a6)
fertig	move.l	_IntBase,a1
	move.l	4.w,a6
	jsr	CloseLibrary(a6)
	rts
ChipMemPointer	dc.l	0
screenpointer	dc.l	0
zaehler	dc.l	0
_IntBase	dc.l	0
IntName	INTNAME
screendata	ds.b	32
x1	dc.w	0
y1	dc.w	0
x2	dc.w	0
y2	dc.w	0
verbase	dc.w	0
verpm	dc.w	0
verbonus	dc.w	0
horbase	dc.w	0
horpm	dc.w	0
horbonus	dc.w	0
xplusminus	dc.w	0
yplusminus	dc.w	0
xnullpunkt	dc.w	160
ynullpunkt	dc.w	100
xbonus	dc.w	0
ybonus	dc.w	0
xdegree	dc.w	0
ydegree	dc.w	0
xfactspeicher	dc.w	0
yfactspeicher	dc.w	0
xmitte	dc.w	113
ymitte	dc.w	20
xaddvar	dc.w	0
yaddvar	dc.w	0
maske	dc.w	%1010101010101010
koordsdata	dc.l	koordsende-koordsdata-4
	;P
	dc.w	$8000+0,40
	dc.w	8,0
	dc.w	23,0
	dc.w	27,5
	dc.w	25,15
	dc.w	19,20
	dc.w	4,20
	;H
	dc.w	$8000+49,0
	dc.w	39,40
	dc.w	$8000+69,0
	dc.w	59,40
	dc.w	$8000+44,20
	dc.w	64,20
	;O
	dc.w	$8000+94,0
	dc.w	88,5
	dc.w	79,35
	dc.w	82,40
	dc.w	92,40
	dc.w	99,35
	dc.w	108,5
	dc.w	104,0
	dc.w	94,0
	;Y
	dc.w	$8000+124,0
	dc.w	120,15
	dc.w	124,20
	dc.w	139,20
	dc.w	$8000+144,0
	dc.w	135,35
	dc.w	129,40
	dc.w	114,40
	;H
	dc.w	$8000+164,0
	dc.w	154,40
	dc.w	$8000+184,0
	dc.w	174,40
	dc.w	$8000+159,20
	dc.w	179,20
koordsende
OktantCodes	dc.b	3*4
	dc.b	7*4
	dc.b	1*4
	dc.b	6*4
	dc.b	2*4
	dc.b	5*4
	dc.b	0*4
	dc.b	4*4
sinusliste	dc.b	0,1,2,3,4,6,7,8,9,10,11,12
grad3	dc.b	13,15,16,17,18,19,20,21,22,23,25,26
grad6	dc.b	27,28,29,30,31,32,33,35,36,37,38,39
grad9	dc.b	40,41,42,43,44,46,47,48,49,50,51,52
grad12	dc.b	53,54,55,56,58,59,60,61,62,63,64,65
grad15	dc.b	66,67,68,69,71,72,73,74,75,76,77,78
grad18	dc.b	79,80,81,82,83,84,85,87,88,89,90,91
grad21	dc.b	92,93,94,95,96,97,98,99,100,101,102,103
grad24	dc.b	104,105,106,107,108,109,110,111,112,113,114,115
grad27	dc.b	116,117,118,119,120,121,122,123,124,125,126,127
grad30	dc.b	127,128,129,130,131,132,133,134,135,136,137,137
grad33	dc.b	138,139,140,141,142,143,144,145,146,147,148,149
grad36	dc.b	149,150,151,152,153,154,155,156,157,157,158,159
grad39	dc.b	160,161,162,163,164,164,165,166,167,168,169,169
grad42	dc.b	170,171,172,173,174,174,175,176,177,178,178,179
grad45	dc.b	180,181,182,182,183,184,185,185,186,187,188,188
grad48	dc.b	189,190,191,191,192,193,194,194,195,196,197,197
grad51	dc.b	198,199,199,200,201,201,202,203,203,204,205,205
grad54	dc.b	206,207,207,208,209,209,210,211,211,212,212,213
grad57	dc.b	214,214,215,216,216,217,217,218,218,219,220,220
grad60	dc.b	221,221,222,222,223,223,224,225,225,226,226,227
grad63	dc.b	227,228,228,229,229,230,230,231,231,231,232,232
grad66	dc.b	233,233,234,234,235,235,236,236,236,237,237,238
grad69	dc.b	238,238,239,239,240,240,240,241,241,241,242,242
grad72	dc.b	242,243,243,243,244,244,244,245,245,245,246,246
grad75	dc.b	246,247,247,247,247,248,248,248,248,249,249,249
grad78	dc.b	249,250,250,250,250,251,251,251,251,251,251,252
grad81	dc.b	252,252,252,252,253,253,253,253,253,253,253,253
grad84	dc.b	254,254,254,254,254,254,254,254,254,254,255,255
grad87	dc.b	255,255,255,255,255,255,255,255,255,255,255,255
grad90	dc.b	255
