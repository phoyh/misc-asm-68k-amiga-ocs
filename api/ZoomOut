	movem.l	d0-7/a0-6,-(sp)
	move.w	90(sp),destxbegin
	move.w	94(sp),destybegin
	move.w	98(sp),d0
	sub.w	destxbegin,d0
	move.w	d0,destlarge
	move.w	102(sp),d0
	sub.w	destybegin,d0
	move.w	d0,desthight
	move.w	70(sp),d0
	lsl.w	#6,d0
	move.w	d0,sourxbegin
	move.l	74(sp),d0
	lsl.w	#6,d0
	move.w	d0,sourybegin
	move.l	64(sp),a0
	move.l	$30(a0),a4
	move.l	4(a4),ColorMap
	move.w	12(a0),d0
	add.w	#7,d0
	lsr.w	#3,d0
	move.w	d0,BytesPerRow
	move.l	88(a0),a4
	move.w	4(a4),tiefe
	move.l	88(a0),a4
	adda.l	#8,a4
	move.l	a4,BitMap
	move.l	84(sp),a0
	move.l	$30(a0),a4
	move.l	4(a4),ColorMap2
	move.w	12(a0),d0
	add.w	#7,d0
	lsr.w	#3,d0
	move.w	d0,BytesPerRow2
	move.l	88(a0),a4
	move.w	4(a4),tiefe2
	move.w	4(a4),d0
	sub.w	#1,d0
	moveq	#1,d1
farb2loop	lsl	d1
	dbf	d0,farb2loop
	move.w	d1,farben2
	move.l	88(a0),a4
	adda.l	#8,a4
	move.l	a4,BitMap2
	clr.l	d0
	move.w	78(sp),d0
	lsl.w	#6,d0
	sub.w	sourxbegin,d0
	divu	destlarge,d0
	move.w	d0,xgo
	clr.l	d0
	move.w	82(sp),d0
	lsl.w	#6,d0
	sub.w	sourybegin,d0
	divu	desthight,d0
	move.w	d0,ygo
	move.w	destxbegin,xtimer
	clr.w	ytimer
	move.w	sourxbegin,xtimersour
	move.w	sourybegin,ytimersour
	move.l	ColorMap,a2
XLoop	move.w	xtimersour,d0
	move.w	d0,d1
	add.w	xgo,d1
	move.w	d1,xtimersour
	lsr.w	#6,d1
	lsr.w	#6,d0
YLoop	move.w	ytimersour,d2
	move.w	d2,d3
	add.w	ygo,d3
	move.w	d3,ytimersour
	lsr.w	#6,d3
	lsr.w	#6,d2
	move.l	BitMap,a5
	jsr	GetMedColour
	jsr	Entsprechung
	move.l	BitMap2,a5
	move.l	ColorMap,a2
	clr.w	d3
	move.w	xtimer,d2
	lsr.w	#3,d2
	move.w	ytimer,d4
	add.w	destybegin,d4
	mulu	BytesPerRow2,d4
	add.w	d4,d2
	move.w	xtimer,d4
	not.w	d4
	and.w	#7,d4
SetPoint	move.l	(a5)+,a0
	btst	d3,d7
	bne	DotIt
	bclr	d4,(a0,d2.w)
	bra	rLoop
DotIt	bset	d4,(a0,d2.w)
rLoop	add.w	#1,d3
	cmp.w	tiefe2,d3
	bne	SetPoint
	add.w	#1,ytimer
	move.w	ytimer,d3
	cmp.w	desthight,d3
	bne	YLoop
	move.w	sourybegin,ytimersour
	clr.w	ytimer
	add.w	#1,xtimer
	move.w	xtimer,d3
	sub.w	destxbegin,d3
	cmp.w	destlarge,d3
	bne	XLoop
Ende	movem.l	(sp)+,d0-7/a0-6
	rts
Entsprechung	move.w	farben2,d7
	sub.w	#1,d7
	move.w	#$FFFF,Minimal
	move.l	ColorMap2,a2
Vergleich	move.w	(a2)+,d2
	move.w	d2,d3
	lsr.w	#8,d3
	and.w	#15,d3
	move.w	d2,d4
	lsr.w	#4,d4
	and.w	#15,d4
	move.w	d2,d5
	and.w	#15,d5
	clr.w	d2
	move.w	Rot,d6
	cmp.w	d3,d6
	bge	Dif1
	move.w	d3,a1
	move.w	d6,d3
	move.w	a1,d6
Dif1	sub.w	d3,d6
	add.w	d6,d2
	move.w	Grun,d6
	cmp.w	d4,d6
	bge	Dif2
	move.w	d4,a1
	move.w	d6,d4
	move.w	a1,d6
Dif2	sub.w	d4,d6
	add.w	d6,d2
	move.w	Blau,d6
	cmp.w	d5,d6
	bge	Dif3
	move.w	d5,a1
	move.w	d6,d5
	move.w	a1,d6
Dif3	sub.w	d5,d6
	add.w	d6,d2
	cmp.w	Minimal,d2
	bhi	EndLoop
	move.w	d2,Minimal
	move.w	d7,d6
	not.w	d6
	move.w	farben2,d2
	sub.w	#1,d2
	and.w	d2,d6
	move.w	d6,bisbester
EndLoop	dbf	d7,Vergleich
	move.w	bisbester,d7
	rts
GetMedColour	clr.w	Rot
	clr.w	Grun
	clr.w	Blau
	move.w	d1,d4
	sub.w	d0,d4
	move.w	d3,d5
	sub.w	d2,d5
	mulu	d5,d4
	move.w	d4,PunktAnzahl
	move.w	d0,xzaehler
	move.w	d2,yzaehler
ReadPoints	move.w	xzaehler,d5
	lsr.w	#3,d5
	move.w	yzaehler,d6
	mulu	BytesPerRow,d6
	add.w	d6,d5
	move.w	xzaehler,d6
	not.w	d6
	and.w	#7,d6
	move.l	a5,a4
	clr.w	d7
	clr.w	d4
LoopIt	move.l	(a4)+,a3
	btst	d6,(a3,d5.w)
	beq	tuz
	bset	d7,d4
tuz	add.w	#1,d7
	cmp.w	tiefe,d7
	bne	LoopIt
	lsl.w	d4
	move.w	(a2,d4.w),d7
	move.w	d7,d6
	lsr.w	#8,d6
	and.w	#15,d6
	add.w	d6,Rot
	move.w	d7,d6
	lsr.w	#4,d6
	and.w	#15,d6
	add.w	d6,Grun
	and.w	#15,d7
	add.w	d7,Blau
	add.w	#1,yzaehler
	cmp.w	yzaehler,d3
	bne	ReadPoints
	move.w	d2,yzaehler
	add.w	#1,xzaehler
	cmp.w	xzaehler,d1
	bne	ReadPoints
	clr.l	d4
	move.w	Rot,d4
	divu	PunktAnzahl,d4
	move.w	d4,Rot
	clr.l	d5
	move.w	Grun,d5
	divu	PunktAnzahl,d5
	move.w	d5,Grun
	clr.l	d5
	move.w	Blau,d5
	divu	PunktAnzahl,d5
	move.w	d5,Blau
	rts
ColorMap	dc.l	0
ColorMap2	dc.l	0
BitMap	dc.l	0
BitMap2	dc.l	0
BytesPerRow	dc.w	0
BytesPerRow2	dc.w	0
tiefe	dc.w	0
tiefe2	dc.w	0
farben2	dc.w	0
Rot	dc.w	0
Grun	dc.w	0
Blau	dc.w	0
sourlarge	dc.w	0
sourhight	dc.w	0
destlarge	dc.w	0
desthight	dc.w	0
sourxbegin	dc.w	0
sourybegin	dc.w	0
destxbegin	dc.w	0
destybegin	dc.w	0
PunktAnzahl	dc.w	0
xzaehler	dc.w	0
yzaehler	dc.w	0
xgo	dc.w	0
ygo	dc.w	0
xtimer	dc.w	0
ytimer	dc.w	0
xtimersour	dc.w	0
ytimersour	dc.w	0
Minimal	dc.w	0
bisbester	dc.w	0
